$PROB    Bolus_1CPT_VVMKM MULTIPLE DOSE FOCE INTER runODE010
$INPUT   ID TIME DV LNDV MDV AMT EVID DOSE VI VMI KMI SD CMT
$DATA    Bolus_1CPTMM.csv IGNORE=@ IGNORE (SD.EQ.1)
$SUBR    ADVAN13 TOL=6
$MODEL   COMP(CENTRAL,DEFOBS)
$PK
         VM=EXP(THETA(1)+ETA(1))
         KM=EXP(THETA(2)+ETA(2))
         V=EXP(THETA(3)+ETA(3))
         S1=V
$DES
         DADT(1)=-VM*(A(1)/V)/(KM+(A(1)/V))
$ERROR
         IPRED = F
         RESCV = THETA(4)
         W     = IPRED*RESCV
         IRES  = DV-IPRED
         IWRES = IRES/W
         Y     = IPRED+W*EPS(1)

$THETA   6.9       ;log VM
$THETA   5.8       ;log KM
$THETA   4.2       ;log Vc
$THETA   (0,0.2,1) ;RSV
$OMEGA   0.1 0.14 0.1
$SIGMA   1 FIX
$EST     NSIG=2 SIGL=6 PRINT=5 MAX=9999 NOABORT POSTHOC METHOD=COND INTER NOOBT
$COV
$TABLE  ID TIME LNDV MDV AMT EVID DOSE VI VMI KMI V VM KM ETA1 ETA2 ETA3
        IPRED IRES IWRES CWRESI
        ONEHEADER NOPRINT FILE=runODE010.csv